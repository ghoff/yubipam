#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.
AC_PREREQ(2.61)
AC_INIT(src/pam_yubikey.c)
AM_INIT_AUTOMAKE(pam_yubikey, 1.0.1)
AC_CONFIG_HEADER(config.h)

# Checks for programs.
AC_PROG_CC
AC_PROG_LIBTOOL

AC_SUBST(INCLUDES)
AC_SUBST(PAMDIR, "/lib/security")

AC_ARG_ENABLE(debug,
	[  --enable-debug           Enable debugging options (bugreports and developers only)],
                 [ if test -n "$GCC"; then
                     # get rid of default autofoo CFLAGS
                     CFLAGS=`echo $CFLAGS | sed -e "s/-O2//" -e "s/-g//"`
                     CFLAGS="$CFLAGS -g -O0 -DDEBUG"
                 else
                     CFLAGS="$CFLAGS -DDEBUG"
                 fi
               ],)

AC_ARG_WITH(pam-dir,
	AC_HELP_STRING([--with-pam-dir=DIR],
			       [Where to install PAM module [[PREFIX/lib/security]]]
				  ),
	[case "${withval}" in
		/*) PAMDIR="${withval}";;
		./*|../*) AC_MSG_ERROR(Bad value for --with-pam-dir);;
		*)  PAMDIR="/lib/${withval}";;
	 esac]
)

AC_ARG_WITH(authdb,
	AC_HELP_STRING([--with-authdb],
				   [Select the default authentication database (default is /etc/yubikey)]),
    [
		AC_SUBST(AUTH_DB_DEFAULT, "$withval")
        AC_DEFINE_UNQUOTED(CONFIG_AUTH_DB_DEFAULT, "$withval")
	],
	[
		AC_SUBST(AUTH_DB_DEFAULT, "/etc/yubikey")
	    AC_DEFINE([CONFIG_AUTH_DB_DEFAULT], ["/etc/yubikey"], [Using default yubikey database])
    ]
)


#AC_MSG_NOTICE([PAM installation path $PAMDIR])

# Checks for libraries.
AC_CHECK_LIB([pam], [pam_start],
			 [], 
			 [AC_MSG_ERROR( [You don't seem to have installed the PAM development package] )]
)

# Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS([stdlib.h string.h syslog.h unistd.h wchar.h security/pam_appl.h security/pam_modules.h security/_pam_macros.h])

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_UINT32_T
AC_TYPE_UINT8_T

# Checks for library functions.
AC_FUNC_MALLOC
AC_FUNC_MEMCMP
AC_FUNC_VPRINTF
AC_CHECK_FUNCS([memset strchr strtol])
AC_CONFIG_FILES([Makefile
                 src/Makefile
				 src/lib/Makefile
				 src/utils/Makefile])
AC_OUTPUT

